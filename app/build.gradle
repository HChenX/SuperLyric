plugins {
    alias(libs.plugins.android.application)
}

android {
    def apk = "SuperLyric"

    Properties properties = new Properties()
    InputStream inputStream = project.rootProject.file('local.properties').newDataInputStream()
    properties.load(inputStream)

    def keyKeyStoreFile = file(properties.getProperty('keyStoreFile'))
    def keyKeyStorePassword = properties.getProperty('keyStorePassword')
    def keyKeyAlias = properties.getProperty('keyAlias')
    def keyKeyAliasPassword = properties.getProperty('keyAliasPassword')

    signingConfigs {
        release {
            storeFile keyKeyStoreFile
            storePassword keyKeyStorePassword
            keyAlias keyKeyAlias
            keyPassword keyKeyAliasPassword
            enableV2Signing true
            enableV3Signing true
            enableV4Signing true
        }

        debug {
            storeFile keyKeyStoreFile
            storePassword keyKeyStorePassword
            keyAlias keyKeyAlias
            keyPassword keyKeyAliasPassword
            enableV2Signing true
            enableV3Signing true
            enableV4Signing true
        }
    }

    namespace 'com.hchen.superlyric'
    compileSdk 35

    defaultConfig {
        applicationId "com.hchen.superlyric"
        minSdk 31
        targetSdk 35
        //noinspection HighAppVersionCode
        versionCode 2025040900
        versionName "1.0"

        ndk {
            // noinspection ChromeOsAbiSupport
            abiFilters 'arm64-v8a', 'armeabi-v7a'
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            minifyEnabled false
            signingConfig signingConfigs.debug
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_21
        targetCompatibility JavaVersion.VERSION_21
    }

    buildFeatures {
        buildConfig true
        aidl true
    }

    aaptOptions.additionalParameters '--allow-reserved-package-id', '--package-id', '0x53'

    android {
        applicationVariants.configureEach { variant ->
            variant.outputs.configureEach { output ->
                outputFileName = "${apk}-v${variant.versionName}-${variant.versionCode}-${variant.buildType.name}.apk"
            }
        }
    }
}

dependencies {
    configurations.configureEach {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
        resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
    }

    compileOnly libs.api
    implementation libs.annotation
    implementation libs.hooktool
    implementation libs.dexkit
    implementation(libs.superlyricapi) {
        changing = true
    }

    implementation(project(':collect'))
    annotationProcessor(project(':collect'))
}